# ğŸ“„ æ•°æ®æ¨¡å‹ Schema, Migration & Branches

**æ•°æ®æ¨¡å‹å®šä¹‰**æ˜¯ä¸šåŠ¡ç³»ç»Ÿçš„åŸºçŸ³, Gel æä¾›å¼ºç±»å‹ã€é«˜çº§çš„ SDL(Schema definition language)è¯­è¨€, ä»¥**åœ¨æ‹›**çš„ä¸šåŠ¡ç³»ç»Ÿä¸ºä¾‹: 

[Login to Gel WebUI for dev server](https://8.140.249.106:5656/ui)

### åŸºç¡€æ¦‚å¿µ

æ•°æ®éƒ½ç”¨ **ç±»å‹å¯¹è±¡(type Object)** æ¥å®šä¹‰:

```python
# å¯ä»¥çœç•¥çš„å…³é”®å­—: propertyã€optionalã€singleã€link
abstract property email_prop {      # Abstract property
    readonly := true;                 # åªè¯»å±æ€§,ä¸å…è®¸ä¿®æ”¹
}

type Player {
    property required email: str {  # str is a Scalar Type
        extending email_prop;
    }; 
    # link ä»£è¡¨ç±»å‹å¯¹è±¡ä¹‹é—´çš„å…³ç³», æ˜¯æœ‰æ–¹å‘çš„, 1-n æˆ– m-n 
    # Player(source) -> multi Game(target)
    optional multi link games: Game {
        # ensures a one-to-many relationship
        constraint exclusive;       # çº¦æŸæ¡ä»¶: å”¯ä¸€
        on source delete allow;     # deletion policy 
    };
}

type Game {
    required name: str {            # required æŒ‡å¿…å¡«å±æ€§
        default := 'Unknown';       # è®¾ç½®é»˜è®¤å€¼
    };
    index on (.name) {              # è®¾ç½®ç´¢å¼•
        annotation description := 'Indexing all users by name.';
    };                  
}
```
- [Properties å±æ€§](https://docs.geldata.com/reference/datamodel/properties) | [Links é“¾æ¥](https://docs.geldata.com/reference/datamodel/links) | [Primitives åŸå§‹ç±»å‹](https://docs.geldata.com/reference/datamodel/primitives)
- [Constraints æ¡ä»¶çº¦æŸ](https://docs.geldata.com/reference/stdlib/constraints#ref-std-constraints) | [ Indexes ç´¢å¼•](https://docs.geldata.com/reference/datamodel/indexes) 
- [Computed properties and links](https://docs.geldata.com/reference/datamodel/computeds) | [Link properties](https://docs.geldata.com/reference/datamodel/linkprops#ref-datamodel-linkprops)

### Abstract æŠ½è±¡ä¸ [Inheritance ç»§æ‰¿](https://docs.geldata.com/reference/datamodel/inheritance)

```python
# declare reusable, user-defined constraint types.
abstract constraint in_range(min: anyreal, max: anyreal) {
  errmessage :=
    'Value must be in range [{min}, {max}].';
  using (min <= __subject__ and __subject__ < max);
}
abstract link link_with_strength {
  strength: int64 {
    constraint in_range(0, 100);
  };
  index on (__subject__@strength);
}
abstract type Person {
  name: str;
  multi friends: Person {
    extending link_with_strength;
  };
}

abstract annotation admin_note; # è‡ªå®šä¹‰æ³¨é‡Š
type Student extending Person {
  overloaded name: str {    # overloaded property
    constraint exclusive;
  }
  overloaded multi friends: Student;
  annotation admin_note := 'system-critical';
}
```

### [Triggers è§¦å‘å™¨](https://docs.geldata.com/reference/datamodel/triggers) ä¸ [Mutation rewrites](https://docs.geldata.com/reference/datamodel/mutation_rewrites)

ç”¨å®¡è®¡æ—¥å¿—(Audit Log)çš„ä¾‹å­æ¥å­¦ä¹ ä½¿ç”¨è§¦å‘å™¨:

```python
type Log {
    action: str;
    timestamp: datetime {
        default := datetime_current();
    }
    target_name: str;
    change: str;
}

type Person {
    required name: str;
    # also for insert or delete
    trigger log_update after update for each
    when (<json>__old__ {**} != <json>__new__ {**})
    do (insert Log {
            action := 'update',
            target_name := __new__.name,
            change := __old__.name ++ '->' ++ __new__.name
        });
}
```
- Triggers may also be used for validation.

æ“ä½œé‡å†™å¯ä»¥æ–¹ä¾¿æˆ‘ä»¬åœ¨æ•°æ®æ’å…¥å’Œæ›´æ–°æ—¶è¿›è¡Œæ‹¦æˆª, å¯¹æ•°æ®è¿›è¡Œä¿®æ”¹;

```python
type Post {
  required title: str;
  required body: str;
  modified: datetime {
    rewrite insert, update using (datetime_of_statement())
  }
}
```

### [Access Policies](https://docs.geldata.com/reference/datamodel/access_policies) å®‰å…¨ç­–ç•¥: Object-level Security

Access policies can greatly simplify your backend code, centralizing access control logic in a single place.

```python
global current_user: uuid;
type User {
  required email: str { constraint exclusive; }
}
type Post {
  required title: str;
  required author: User;

  access policy author_has_full_access
    allow all
    using (global current_user ?= .author.id) {
      errmessage := "User does not have full access";
    }
}
# Policy types: all | select | insert | update | delete | update read | update write
```

### å·¥å…·èƒ½åŠ›: [Aliases](https://docs.geldata.com/reference/datamodel/aliases)ã€[Globals](https://docs.geldata.com/reference/datamodel/globals)ã€[Functions](https://docs.geldata.com/reference/datamodel/functions)ã€[Annotations](https://docs.geldata.com/reference/datamodel/annotations)

```python
# Alias
alias digits := {0,1,2,3,4,5,6,7,8,9};
alias Account := User;

# Globals
required global now := datetime_of_transaction();
global current_user_id: uuid;
global current_user := (
  select User filter .id = global current_user_id
);

# Functions
function add_user(name: str) -> User
  using (
    insert User {
      name := name,
      joined_at := std::datetime_current(),
    }
  );

# Annotations 
type Status {
  annotation title := 'Activity status';
  annotation description := 'All possible user activities';

  required name: str {
    constraint exclusive
  }
}
```

### æ•°æ®åº“æ‰©å±• [Extensions](https://docs.geldata.com/reference/datamodel/extensions#create-extension)

```bash
gel extension list
```

## **Migrations** 

å¯ä»¥è®°å½•æ•°æ®åº“ Schema çš„å˜æ›´å†å², éšæ—¶å›æ»š, å¹¶å°†æ¨¡å‹ SDL è‡ªåŠ¨è½¬æ¢ä¸ºæ•°æ®åº“å¯ä»¥ç†è§£çš„ DDL(data definition language) åºåˆ—, æ–¹ä¾¿åœ¨å¼€å‘è¿‡ç¨‹ä¸­è½»æ¾çš„æ”¹è¿›æ•°æ®æ¨¡å‹. 

ğŸ‘‰ [Guide to Gel migrations](https://docs.geldata.com/resources/guides/migrations)

```bash
gel migration create    # ç”Ÿæˆè¿ç§»è®°å½•
gel migrate             # åº”ç”¨è¿ç§»åˆ°æ•°æ®åº“
```

## **Branch** 

ä»£ç ä»“åº“åˆ†æ”¯çš„æ¦‚å¿µåŒç†, å¯ä»¥ä½¿ç”¨ ```gel branch``` å‘½ä»¤æ–¹ä¾¿åœ°åˆ›å»ºã€åˆ é™¤å’Œåˆ‡æ¢åˆ†æ”¯, ç®€åŒ–å¼€å‘åä½œæµç¨‹. 

ğŸ‘‰ [Branches references](https://docs.geldata.com/reference/datamodel/branches#ref-datamodel-branches)

## **Module** 

å¯ä»¥ç†è§£ä¸ºä¸€ä¸ªå‘½åç©ºé—´, å¤æ‚é¡¹ç›®å¯ä»¥ç”¨ module æ›´å¥½çš„åˆ’åˆ† Schema å®šä¹‰. 

ğŸ‘‰ [Module references](https://docs.geldata.com/reference/datamodel/modules#ref-datamodel-modules)